apply plugin: 'com.android.application'
apply plugin: 'jacoco'

allprojects {
    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenLocal()
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }
}

group = "org.jesperancinha.itf"
version = "2.0.0-SNAPSHOT"


android {
    namespace 'org.jesperancinha.itf.android'
    defaultConfig {
        compileSdk 34
        applicationId "org.jesperancinha.itf.android"
        minSdkVersion 34
        targetSdkVersion 34
        versionCode 2
        versionName "2.0.0-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        dev {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    sourceSets {
        main {
            manifest.srcFile 'src/manifests/AndroidManifest.xml'
            java.srcDirs(['src/main/java'])
            res.srcDirs(['src/main/res'])
        }
    }
}

tasks.withType(Test).configureEach {
    project.gradle.startParameter.excludedTaskNames.add('packageDev')
    project.gradle.startParameter.excludedTaskNames.add('packageDebug')
    project.gradle.startParameter.excludedTaskNames.add('packageRelease')
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn tasks.withType(Test)
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.github.QuadFlask:colorpicker:0.0.15') {
        exclude group: "com.android.support"
    }
    implementation 'org.jesperancinha.itf:itf-chartizate-android:6.0.1'
    implementation 'org.jesperancinha.itf:itf-chartizate-api:5.0.1'
    implementation 'androidx.fragment:fragment:1.8.8'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testImplementation(platform('org.junit:junit-bom:5.13.4'))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.platform:junit-platform-launcher")
    testImplementation("org.junit.platform:junit-platform-engine")
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    androidTestImplementation(platform('org.junit:junit-bom:5.13.4'))
    androidTestImplementation("org.junit.jupiter:junit-jupiter-api")
    androidTestImplementation("org.junit.jupiter:junit-jupiter-engine")
    androidTestImplementation("org.junit.platform:junit-platform-launcher")
    androidTestImplementation("org.junit.platform:junit-platform-engine")
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'org.mockito:mockito-android:5.18.0'
    androidTestUtil 'androidx.test:orchestrator:1.5.1'

}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name.startsWith('kotlin-stdlib')) {
                details.useVersion 'latest.release'
            }
            if (details.requested.group == 'org.jetbrains.kotlinx' && details.requested.name.startsWith('kotlinx-coroutines')) {
                details.useVersion 'latest.release'
            }
        }
    }
}
